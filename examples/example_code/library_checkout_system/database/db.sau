[use "os"]
[import "database/entry.sau"]

[var database::temp_db_file "tmp.db"]

[var database::load [lambda [file] [block 
   [if [not [os.is_file file]] [[block
      [putln "Given item is not a file: " file]
      [exit 1]
   ]]]

   [var file_data [os.file_read file]]

   [if [is_nil file_data] [block 
      [putln "Unable to read file: " file]
      [exit 1]
   ]]

   [var books]
   [if [ > [len file_data] 0 ] [block
      [var i 0]
      [var j 0]
      [var temp_list]
      [loop [> [len file_data] i] [set i [+ 1 i]] [block
         [push temp_list [at i file_data]]
         [set j [+ 1 j]]
         [if [== 3 j] [block
            [set j 0]
            [push books [database::new_entry [at 0 temp_list] [at 1 temp_list] [at 2 temp_list]]]
            [clear temp_list]
         ]]
      ]] ; end loop

   ; Else
   ][putln "Empty database file"]]

   [books]
]]]

[var database::save [lambda [file data] [block
   [os.file_write database::temp_db_file ""]
   [iter x data
      [os.file_append database::temp_db_file
         [fmt.encode "%\n%\n%\n" [list x.title x.description x.status]]
      ]
   ]
   [if [os.exists file] [os.delete file]]
   [os.copy database::temp_db_file file [os.copy_flags 0 1 0]]
   [os.delete database::temp_db_file]
]]]